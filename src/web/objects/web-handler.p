/*********************************************************************
* Copyright (C) 2005-2016 by Progress Software Corporation. All rights*
* reserved.  Prior versions of this work may contain portions        *
* contributed by participants of Possenet.                           *
*                                                                    *
*********************************************************************/
/*------------------------------------------------------------------------
  Purpose: Handles PAS web requests for classic WebSPeed
           This is a PAS variation of web-disp.p.  
           The main difference is that the events are handled by PAS 
           and passed to the process-web-request procedure, which 
           manages the request the same way that web-disp.p does. 
        -  This uses webutil/paswebstart.pm which includes the   
           classic webstart.p, but extends/replaces standard logic 
           that is be done differently for PAS (read of environment 
           variables.)  
------------------------------------------------------------------------*/
{ src/web/method/cgidefs.i  NEW } /* standard WS cgidefs.i: functions,vars */
{ src/web/method/cgiarray.i NEW } /* standard WS cgiarray.i: vars          */
{ src/web/method/tagmap.i   NEW } /* standard WS tagmap.i: TT tagmap       */
{ src/web/method/webutils.i NEW }

&SCOPED-DEFINE EXCLUSIVE-WEB-USER EXCLUSIVE-WEB-USER

/* Also defined in web/objects/web-util.p and adeuib/_semain.w.  
   This needs to be centralized. */
define new shared variable server-connection as character no-undo.
define new shared variable transaction-state as character no-undo.


/* set trigger to clean up the procedure */
on "close":U of this-procedure 
do:
   run destroyObject.
end.
    
/* Load standard and user-defined super procedures.  web/objects/web-util.p and 
   init-session runs within.  This program is set as web-utilities-hdl. */
run webutil/paswebstart.p persistent.

procedure process-web-request :
    define variable cMimeCharset       as character  no-undo.
    define variable cProCharset        as character  no-undo.
    define variable iTest              as integer    no-undo.
        
    output {&WEBSTREAM} TO "WEB":U.
    
    /* This MUST be the first thing written */
    output-http-header("", "HTTP/1.1 200 OK":U).
      
    /* Parse the request/CGI from the web server. */
    run init-cgi in web-utilities-hdl.

    /* Initialize for web-request. */
    run init-request in web-utilities-hdl.

    &IF KEYWORD-ALL("HTML-CHARSET") <> ? &THEN  
    assign
       cMimeCharset = get-value("wscharset":U)
       cProCharset  = ""
       iTest        = 0.
    
    if cMimeCharset <> ? and cMimeCharset <> "" then 
    do:
        /* Confirm receipt of valid MIME character set */
        run adecomm/convcp.p (cMimeCharset, "toProg":U, output cProCharset) no-error.
        if cProCharset = "" then
          message substitute("The &1 character set has no Progress equivalent.",
                             cMimeCharset).
        else do:
          assign iTest = asc("A":U, session:cpinternal, cProCharset) NO-ERROR.
          if iTest > 0 then
            web-context:html-charset = cProCharset.
        end.
    
        if iTest >= 0 and web-context:html-charset <> "" 
          and web-context:html-charset <> session:cpstream then
          output {&WEBSTREAM} TO "WEB":U CONVERT TARGET web-context:html-charset.
    end.
    &ENDIF
    AppProgram = (if AppProgram = "debug":U then "webutil/debug.p":U else
                 (if AppProgram = "ping":U  then "webutil/ping.p":U  else
                 (if AppProgram = "reset":U then "webutil/reset.p":U else
                  AppProgram))).
  
    
    run run-web-object in web-utilities-hdl (AppProgram) no-error. 

    /* Run clean up and maintenance code */
    run end-request in web-utilities-hdl no-error.
  
    /* If any debugging options are set except "top" ... */
    if debugging-enabled and debug-options <> "" and LOOKUP("top":U,debug-options) = 0 
    and     /* don't do it for these procedures */
        lookup(AppProgram,"webutil/debug.p,webutil/reset.p,webutil/ping.p":U) = 0 then
    run web/support/printval.p (debug-options).

    /* Output any pending messages queued up by queue-message() */
    if available-messages(?) then
        output-messages("all", ?, "Messages:").

    /* Output an HTML "... generated by ..." comment at the end of every
       output page but only if HTML is being output. The comment was originally at then beginning of 
       the page but unfortunately IE has a problem with the DOCTYPE not being the first tag
       in standards mode such as XHTML.
    */
    if can-do ("text/html*,text/x-server-parsed-html*":U,output-content-type) then
        {&OUT} "~n~n<!-- Generated by Webspeed: http://www.webspeed.com/ -->~n":U.
  
    finally:
        output {&WEBSTREAM} CLOSE.
    end.
end procedure. /* process-web-request */

procedure destroyObject:
    if valid-handle(web-utilities-hdl) then 
        apply "close" to web-utilities-hdl.
    delete procedure web-utilities-hdl no-error.     
    delete procedure this-procedure.    
end.    
